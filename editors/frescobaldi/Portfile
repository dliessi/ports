# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           python 1.0
PortGroup           github 1.0

github.setup        wbsoft frescobaldi 2.0.10 v
revision            1
conflicts           frescobaldi-devel
categories          editors python
maintainers         gmail.com:davide.liessi openmaintainer
description         A LilyPond sheet music text editor
long_description    Frescobaldi is a LilyPond sheet music text editor. \
                    It aims to be powerful, yet lightweight and easy to use.
homepage            http://www.frescobaldi.org/
platforms           darwin
supported_archs     noarch
license             GPL-2+

checksums           rmd160  197dc2ac14d6570fd2f5a9e04ea50a5a4a3e8b73 \
                    sha256  1e8bfe2911057b066e5af54e685d786153956b436a8a6d6d50868c50c180049f

build {}

depends_run-append  port:portmidi

variant python26 conflicts python27 description {Use Python 2.6} {
    python.default_version  26
    depends_run-append      port:py${python.default_version}-pyqt4 \
                            port:py${python.default_version}-python-poppler-qt4
}
variant python27 conflicts python26 description {Use Python 2.7} {
    python.default_version  27
    depends_run-append      port:py${python.default_version}-pyqt4 \
                            port:py${python.default_version}-python-poppler-qt4
}
if {![variant_isset python26]} {
    default_variants        +python27
}

notes \
"You may want to install FluidSynth and obtain a SoundFont
in order to have audio output from Frescobaldi's MIDI output.

You will need to select FluidSynth's MIDI input port
in Frescobaldi's MIDI settings (in Preferences)
while FluidSynth is running."


subport frescobaldi-devel {
    github.setup        wbsoft frescobaldi c806f38b7e8c960a5fe024fa620d1206306b8398
    
    # github.setup sets the port name to frescobaldi, overriding the subport name.
    # With 'name ${subport}', `port lint frescobaldi-devel` gives the following error:
    # 'Error: Portfile directory frescobaldi does not match port name frescobaldi-devel'.
    # With 'PortInfo(name) ${subport}' (see the definition of 'proc subport' in portutil.tcl)
    # the Portfile works correctly and the error disappears.
    global PortInfo
    set PortInfo(name) ${subport}
    
    conflicts           frescobaldi
    version             20131015
    revision            0
    
    checksums           rmd160  65d0e2d1fedbb3319fef1e01a55dd491928a8655 \
                        sha256  01dd7d0a7835b8cd9e294380dd9e0b240e29ae12baaaec4d77fac662c7fcc2fb
    
    variant app description {Make application bundle} {
        depends_build-append    port:py${python.default_version}-cx_Freeze
    }
    
    default_variants    +app
}


post-destroot {
    if {${subport} ne "frescobaldi"} {
        set man_dir ${destroot}${prefix}/share/man/man1
        xinstall -d ${man_dir}
        xinstall -m 644 -W ${worksrcpath} \
            frescobaldi.1 \
            ${man_dir}
    }
    
    set doc_dir ${destroot}${prefix}/share/doc/${subport}
    xinstall -d ${doc_dir}
    xinstall -m 644 -W ${worksrcpath} \
        COPYING \
        ChangeLog \
        README \
        THANKS \
        ${doc_dir}
    
    if {[variant_isset app]} {
        set app_contents    ${destroot}${applications_dir}/Frescobaldi.app/Contents
        set app_macos       ${app_contents}/MacOS
        set app_resources   ${app_contents}/Resources
        
        set infopliststrings \
"/* Localized versions of Info.plist keys */

CFBundleName = \"Frescobaldi\";
CFBundleDisplayName = \"Frescobaldi\";

NSHumanReadableCopyright = \"Copyright © 2008-2012 Wilbert Berendsen.\";
"
        foreach l {cs de en es fr gl it nl pl pt ru tr uk} {
            set app_lproj ${app_resources}/${l}.lproj
            xinstall -d ${app_lproj}
            set fileID [ open "${app_lproj}/InfoPlist.strings" "w" ]
            puts -nonewline ${fileID} ${infopliststrings}
            close ${fileID}
        }
        
        set infoplist \
"<?xml version=\"1.0\" encoding=\"UTF-8\"?>
<!DOCTYPE plist PUBLIC \"-//Apple//DTD PLIST 1.0//EN\" \"http://www.apple.com/DTDs/PropertyList-1.0.dtd\">
<plist version=\"1.0\">
<dict>
    <key>CFBundlePackageType</key>
    <string>APPL</string>
    <key>CFBundleIdentifier</key>
    <string>org.frescobaldi.frescobaldi</string>
    <key>CFBundleSignature</key>
    <string>????</string>
    <key>CFBundleExecutable</key>
    <string>Frescobaldi</string>
    <key>CFBundleDisplayName</key>
    <string>Frescobaldi</string>
    <key>CFBundleVersion</key>
    <string>${version}</string>
    <key>CFBundleName</key>
    <string>Frescobaldi</string>
    <key>CFBundleIconFile</key>
    <string>frescobaldi.icns</string>
    <key>NSHumanReadableCopyright</key>
    <string>Copyright © 2008-2012 Wilbert Berendsen.</string>
</dict>
</plist>
"
        set fileID [ open "${app_contents}/Info.plist" "w" ]
        puts -nonewline ${fileID} ${infoplist}
        close ${fileID}
        
        xinstall -d ${app_macos}
        exec ${prefix}/bin/cxfreeze-${python.branch} \
            ${destroot}${frameworks_dir}/Python.framework/Versions/${python.branch}/bin/frescobaldi \
            --target-dir ${app_macos} --no-copy-deps --target-name Frescobaldi
        
        xinstall -m 644 -W ${filespath} frescobaldi.icns ${app_resources}
    }
}
