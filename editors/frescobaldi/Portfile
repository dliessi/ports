# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           python 1.0
PortGroup           github 1.0

github.setup        wbsoft frescobaldi 2.0.11 v
revision            1
conflicts           frescobaldi-devel
categories          editors python
maintainers         gmail.com:davide.liessi openmaintainer
description         A LilyPond sheet music text editor
long_description    Frescobaldi is a LilyPond sheet music text editor. \
                    It aims to be powerful, yet lightweight and easy to use.
homepage            http://www.frescobaldi.org/
platforms           darwin
supported_archs     noarch
license             GPL-2+

checksums           rmd160  5033eef310d5b68de3951cf07517416436f47cb7 \
                    sha256  478177a1292e1232df7fb11cae3a28e67af2186191ad1f976e390029e331139c

subport frescobaldi-devel {
    github.setup        wbsoft frescobaldi 48648463dc86195cb0245b99117f9352b50e1581
    
    # github.setup sets the port name to frescobaldi, overriding the subport name.
    # With 'name ${subport}', `port lint frescobaldi-devel` gives the following error:
    # 'Error: Portfile directory frescobaldi does not match port name frescobaldi-devel'.
    # With 'PortInfo(name) ${subport}' (see the definition of 'proc subport' in portutil.tcl)
    # the Portfile works correctly and the error disappears.
    global PortInfo
    set PortInfo(name) ${subport}
    
    conflicts           frescobaldi
    version             20131021
    revision            0
    
    checksums           rmd160  d493a23f5c506d9631b8bb2533b5e125fec0f2b0 \
                        sha256  09cc193476e6995066fa87a5deffa5e1285d78a5d6929a7925be44ed929f3aa6
}

build {}

depends_run-append  port:portmidi

variant python26 conflicts python27 description {Use Python 2.6} {
    python.default_version  26
    depends_run-append      port:py${python.default_version}-pyqt4 \
                            port:py${python.default_version}-python-poppler-qt4
}
variant python27 conflicts python26 description {Use Python 2.7} {
    python.default_version  27
    depends_run-append      port:py${python.default_version}-pyqt4 \
                            port:py${python.default_version}-python-poppler-qt4
}
if {![variant_isset python26]} {
    default_variants        +python27
}

variant app description {Make application bundle} {
    depends_build-append    port:py${python.default_version}-cx_Freeze
}
default_variants    +app

post-destroot {
    set share_destroot ${destroot}${prefix}/share
    set share_python ${python.prefix}/share

    set man_dir /man/man1
    xinstall -d ${share_destroot}${man_dir}
    ln -s ${share_python}${man_dir}/frescobaldi.1 ${share_destroot}${man_dir}

    set desktop_dir /applications
    xinstall -d ${share_destroot}${desktop_dir}
    ln -s ${share_python}${desktop_dir}/frescobaldi.desktop ${share_destroot}${desktop_dir}

    set icons_dir /icons/hicolor/scalable/apps
    xinstall -d ${share_destroot}${icons_dir}
    ln -s ${share_python}${icons_dir}/frescobaldi.svg ${share_destroot}${icons_dir}

    set doc_dir ${share_destroot}/doc/${subport}
    xinstall -d ${doc_dir}
    xinstall -m 644 -W ${worksrcpath} \
        COPYING \
        ChangeLog \
        README \
        THANKS \
        ${doc_dir}

    if {[variant_isset app]} {
        set app_contents    ${destroot}${applications_dir}/Frescobaldi.app/Contents
        set app_macos       ${app_contents}/MacOS
        set app_resources   ${app_contents}/Resources

        foreach l {cs de en es fr gl it nl pl pt ru tr uk} {
            set app_lproj ${app_resources}/${l}.lproj
            xinstall -d ${app_lproj}
            xinstall -m 644 -W ${filespath} InfoPlist.strings ${app_lproj}
        }

        xinstall -m 644 -W ${filespath} Info.plist ${app_contents}
        reinplace "s|%VERSION%|${version}|g" ${app_contents}/Info.plist

        xinstall -d ${app_macos}
        exec ${prefix}/bin/cxfreeze-${python.branch} \
            ${destroot}${python.prefix}/bin/frescobaldi \
            --target-dir ${app_macos} --no-copy-deps --target-name Frescobaldi

        xinstall -m 644 -W ${filespath} frescobaldi.icns ${app_resources}
    }
}

python.link_binaries_suffix

notes \
"You may want to install FluidSynth and obtain a SoundFont
in order to have audio output from Frescobaldi's MIDI output.

You will need to select FluidSynth's MIDI input port
in Frescobaldi's MIDI settings (in Preferences)
while FluidSynth is running."
